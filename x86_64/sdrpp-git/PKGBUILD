# Maintainer: Alexandre Rouma <whatsthetgeek@gmail.com>
# Maintainer: Ã©clairevoyant

_pkgname=sdrpp
pkgname="$_pkgname-git"
pkgver=1.0.4.r471.71327cd
pkgrel=4
pkgdesc="The Bloat-free SDR Receiver"
arch=(x86_64)
url="https://github.com/AlexandreRouma/SDRPlusPlus"
license=(GPL3)
depends=(fftw glfw glew libvolk rtaudio)
_plugindeps=(airspy bladerf hackrf libad9361 libiio limesuite rtl-sdr soapysdr codec2)
makedepends=(cmake git "${_plugindeps[@]}")
optdepends=("${_plugindeps[@]}")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+$url.git")
b2sums=('SKIP')

pkgver() {
	cd $_pkgname
	git describe --long --tags --exclude nightly | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
	cmake -B build -S $_pkgname \
		-DOPT_BUILD_AIRSPYHF_SOURCE=OFF \
        -DOPT_BUILD_SDRPLAY_SOURCE=OFF \
        -DOPT_BUILD_BLADERF_SOURCE=OFF \
        -DOPT_BUILD_LIMESDR_SOURCE=OFF \
        -DOPT_BUILD_SCHEDULER=ON \
        -DOPT_BUILD_M17_DECODER=ON
	make -C build $MAKEOPTS
}

package() {
	make -C build DESTDIR="$pkgdir/" install
}
